plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'net.darkhax.curseforgegradle' version '1.0.14'
    id 'org.spongepowered.mixin' version '0.7.+'
}

apply {
    from 'https://files.saps.dev/ci/git-md-changelog.gradle'
}

def ENV = System.getenv()

version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}+mc${minecraft_version}"
group = 'dev.wuffs' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'BetterCompatibilityChecker'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: "${minecraft_version}"

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=bcc.mixins.json"

            mods {
                bcc {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=bcc.mixins.json"

            mods {
                bcc {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'bcc', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                bcc {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "bcc.refmap.json"

    debug.verbose = true
    debug.export = true
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "bcc",
                "Specification-Vendor"    : "Gaz",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "gaz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "bcc.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar) {
    setArchiveClassifier("sources")
    from sourceSets.main.allJava
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
        if (ENV.NANITE_DEPLOY) {
            maven {
                url "https://maven.nanite.dev/"
                credentials {
                    username = "nanite"
                    password = "${ENV.NANITE_TOKEN}"
                }
            }
        }
    }
}


// CurseForgeGradle

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {

    apiToken = ENV.CURSE_TOKEN

    // The main file to upload
    def mainFile = upload(project.curseforge_id, jar)
    mainFile.releaseType = 'release'
    mainFile.changelog = file("./CHANGELOG.md")
    mainFile.changelogType = 'markdown'
    mainFile.addJavaVersion("Java 17")
    mainFile.addGameVersion(project.minecraft_version)
    mainFile.addModLoader("Forge")
//    mainFile.addGameVersion("Server")
//    mainFile.addGameVersion("Client")
}
//    curseforge {
//        apiKey = ENV.CURSE_TOKEN
//        project {
//            id = project.curseforge_id
//            releaseType = "release"
//            addGameVersion "Forge"
//            addGameVersion "${minecraft_version}"
//            mainArtifact(jar)
//            changelog = file("./CHANGELOG.md")
//            changelogType = 'markdown'
//        }
//    }
//}

tasks.register('lazyMikey') { task ->
    doLast {
        def mcVersion = "1.20"
        def conn = new URL('https://maven.minecraftforge.net/net/minecraftforge/forge/maven-metadata.xml').openConnection()
        def forgeXml = new XmlParser().parse(conn.getInputStream())
        println(forgeXml.versioning.versions.version.findAll { it.text().startsWith(mcVersion) }.collect { it.text() }.first())
    }
}